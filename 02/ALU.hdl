// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=zerox); // zx
    Mux16(a=y, b=false, sel=zy, out=zeroy); // zy

    Not16(in=zerox, out=negx); // prep notx
    Mux16(a=zerox, b=negx, sel=nx, out=notx); // nx

    Not16(in=zeroy, out=negy); // prep noty
    Mux16(a=zeroy, b=negy, sel=ny, out=noty); // ny

    Add16(a=notx, b=noty, out=addxy); // x + y
    And16(a=notx, b=noty, out=andxy); // x & y

    Mux16(a=andxy, b=addxy, sel=f, out=output); // f
    Not16(in=output, out=noutput); // prep the negated output

    Mux16(a=output, b=noutput, sel=no, out=out, out[0..7]=o1, out[8..15]=o2, out[15]=ng); // negate the output or nah

    Or8Way(in=o1, out=hasOne1);
    Or8Way(in=o2, out=hasOne2);
    Or(a=hasOne1, b=hasOne2, out=hasOne); // check if its not all zeros
    Not(in=hasOne, out=zr);
}
